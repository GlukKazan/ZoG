(define drop-pass (
  (verify (in-zone? pass-zone))
  add
))

(define clear-pass
  (if (not-empty? p1)
      (capture p1)
  )
)

(define common-stop
  (clear-pass)
  (if enemy?
      (verify (not-piece? Pikeman))
  )
  (if (and (in-zone? var a2)
           (in-zone? goal))
      (add Pikeman)
   else
      (add $1)
  )
)

(define step-1 (
  $1 (verify not-friend?)
  (verify (not-piece? Mount))
  (common-stop $2)
))

(define step-2 (
  $1 (verify empty?)
  $2 (verify not-friend?)
  (verify (not-piece? Mount))
  (common-stop $3)
))

(define jump-2 (
  $1 (verify (not-piece? Mount))
  $2 (verify not-friend?)
  (verify (not-piece? Mount))
  (common-stop $3)
))

(define slide-1 (
  $1 (while empty?
     (verify (not-piece? Mount))
     (common-stop $2)
     $1
  )
  (verify not-friend?)
  (verify (not-piece? Mount))
  (common-stop $2)
))

(define slide-2 (
  $1 (verify (not-piece? Mount))
  $1 (while empty?
     (verify (not-piece? Mount))
     (common-stop $2)
     $1
  )
  (verify not-friend?)
  (verify (not-piece? Mount))
  (common-stop $2)
))

(define strike-1 (
  mark 
  $1 (verify enemy?)
  to capture back
  (create $2) 
  (clear-pass) add
))

(define strike-2 (
  mark 
  $1 (verify (not-piece? Mount))
  $2 (verify enemy?)
  to capture back
  (create $3) 
  (clear-pass) add
))

(define strike-3 (
  mark 
  $1 (verify (not-piece? Mount))
  $2 (verify (not-piece? Mount))
  $3 (verify enemy?)
  to capture back
  (create $4) 
  (clear-pass) add
))

(define order-1 (
  $1 (verify friend?)
  (verify (piece? $2R))
  (change-type $2A)
  $3 (verify not-friend?)
  (verify (not-piece? Mount))
  (clear-pass) add
))

(define order-1? (
  $1 (verify friend?)
  (verify (piece? $2R))
  (verify (not-piece? Mount $4))
  (change-type $2A)
  $3 (verify not-friend?)
  (verify (not-piece? Mount))
  (clear-pass) add
))

(define order-11 (
  $1 (verify friend?)
  (verify (or (piece? $2R) (piece? $2A)))
  (if (piece? $2R)
      (change-type $2A)
   else
      (change-type $2R)
  )
  $3 (verify not-friend?)
  (verify (not-piece? Mount))
  (clear-pass) add
))

(define order-12 (
  $1 (verify friend?)
  (verify (or (piece? $2R) (piece? $2A)))
  (if (piece? $2R)
      (change-type $2A)
   else
      (change-type $2R)
  )
  $3 (verify (not-piece? Mount))
  $4 (verify not-friend?)
  (verify (not-piece? Mount))
  (clear-pass) add
))

(define order-21 (
  $1 (verify (not-piece? Mount))
  $2 (verify friend?)
  (verify (or (piece? $3R) (piece? $3A)))
  (if (piece? $3R)
      (change-type $3A)
   else
      (change-type $3R)
  )
  $4 (verify not-friend?)
  (verify (not-piece? Mount))
  (clear-pass) add
))

(define order-22 (
  $1 (verify (not-piece? Mount))
  $2 (verify friend?)
  (verify (or (piece? $3R) (piece? $3A)))
  (if (piece? $3R)
      (change-type $3A)
   else
      (change-type $3R)
  )
  $4 (verify (not-piece? Mount))
  $5 (verify not-friend?)
  (verify (not-piece? Mount))
  (clear-pass) add
))

(define order-def
  (order-1 nw General w) (order-1 nw General e) (order-1 nw General s) (order-1? nw General sw s)
  (order-1 ne General w) (order-1 ne General e) (order-1 ne General s) (order-1? ne General se s)
  (order-1 n  General w) (order-1 n General  e) (order-1 n General sw) (order-1 n General se) 
  (order-1 w  General s) (order-1 w General  w) (order-1 w General sw) (order-1 w General se) 
  (order-1 e  General s) (order-1 e General  e) (order-1 e General sw) (order-1 e General se)
  (order-1 sw Marshall n) (order-1? sw Marshall nw n)
  (order-1 se Marshall n) (order-1? se Marshall ne n)
  (order-1 s Marshall nw) (order-1 s Marshall ne)
  (order-11 w Duchess w) (order-11 e Duchess e) 
  (order-21 w w Duchess w) (order-21 w w Duchess e) (order-21 e e Duchess e) (order-21 e e Duchess w)
  (order-12 w Duchess w w) (order-12 e Duchess w w) (order-12 e Duchess e e) (order-12 w Duchess e e)
  (order-22 w w Duchess w w) (order-22 e e Duchess e e) 
)

(define drop-home (
  (verify (in-zone? home))
  (verify empty?)
  add
))

(define check-duke-dir
  (if (on-board? $1)
      (if (and (friend? $1)
          (or (piece? DukeA $1) (piece? DukeR $1)))
          (set-flag is-duke-neared? true)
      )
  )
)

(define check-duke
  (set-flag is-duke-neared? false)
  (check-duke-dir n)
  (check-duke-dir s)
  (check-duke-dir w)
  (check-duke-dir e)
  (verify (flag? is-duke-neared?))
)

(define drop-near (
  (verify empty?)
  (check-duke)
  add
))

(define drop-blank (
  mark a0 nx
  (while (and (on-board? nx) (not-piece? $1)) nx)
  (verify (piece? $1))
  capture back
  (verify (not-in-zone? pass-zone))
  (verify (piece? Blank))
  (clear-pass) add
))

(define board-def
  (board
    (image "../images/Duke/board.bmp")
    (positions
      (p1 0 -10 5 5)
    )
    (grid
      (start-rectangle 3 4 121 120)
      (dimensions
        ("a/b/c/d/e/f" (118 0)) ; files
        ("6/5/4/3/2/1" (0 116)) ; ranks
      )
      (directions (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
        (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
      )
    )
    (grid
      (start-rectangle -10 0 -5 5)
      (dimensions
        ("S/R" (5 0)) ; files
        ("16/15/14/13/12/11/10/9/8/7/6/5/4/3/2/1" (0 5)) ; ranks
      )
      (directions (nx 0 1))
    )
    (dummy a0)
    (links nx (a0 S16) (S1 R16))
    (symmetry North (n s) (s n) (e w) (w e) (nw se) (se nw) (ne sw) (sw ne))
    (zone (name home) (players South)
          (positions a1 b1 c1 d1 e1 f1)
    )
    (zone (name home) (players North)
          (positions a6 b6 c6 d6 e6 f6)
    )
    (zone (name goal) (players South)
          (positions a6 b6 c6 d6 e6 f6)
    )
    (zone (name goal) (players North)
          (positions a0)
    )
    (zone (name var) (players North South)
          (positions $1)
    )
    (zone (name pass-zone) (players ?R South North)
          (positions p1)
    )
  )
)

(game
  (title "Duke")

  (option "animate captures" false)
  (option "animate drops"    false)
  (option "pass turn"        forced)   ; false
  (option "highlight goals"  false)
  (option "prevent flipping" true)
  (option "recycle captures" false)    ; true for drop-pass
  (option "smart moves"      false)

  (move-sound    "Audio/Clack.wav")
  (release-sound "Audio/Clack.wav")
  (drop-sound    "Audio/Clack.wav")
  (capture-sound "")
  (change-sound  "")
  (click-sound   "")
  (draw-sound    "")

  (players South North ?R)
  (turn-order 
     (South setup-duke)
     (South setup-footman)
     (South setup-footman)
     (North setup-duke)
     (North setup-footman)
     (North setup-footman)
     repeat
     (South normal)
     (?R South blank-south)
     (North normal)
     (?R North blank-north)
  )
  (board-def a1)

  (piece
     (name  Blank)
     (image South "../images/Duke/i.bmp"
            North "../images/Duke/i.bmp")
     (drops
        (move-type normal)
        (drop-near)
;       (move-type blank-south)
;       (drop-pass)
;       (move-type blank-north)
;       (drop-pass)
     )
  )
  (piece
     (name  Mount)
     (image South "../images/Duke/m.bmp"
            North "../images/Duke/m.bmp")
  )

  (piece
     (name  DukeA)
     (notation "Duke")
     (image South "../images/Duke/kas.bmp"
            North "../images/Duke/kan.bmp")
     (moves
        (move-type normal)
        (slide-1 w DukeR) (slide-1 e DukeR)
        (order-def)
     )
     (drops
        (move-type setup-duke)
        (drop-home)
     )
  )
  (piece
     (name  DukeR)
     (notation "Duke")
     (image South "../images/Duke/krs.bmp"
            North "../images/Duke/krn.bmp")
     (moves
        (move-type normal)
        (slide-1 n DukeA) (slide-1 s DukeA)
        (order-def)
     )
  )
  (piece
     (name  AssassinA)
     (notation "Assassin")
     (image South "../images/Duke/aas.bmp"
            North "../images/Duke/aan.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (slide-2 n AssassinR) (slide-2 sw AssassinR) (slide-2 se AssassinR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank AssassinA)
        (move-type blank-north)
        (drop-blank AssassinR)
     )
  )
  (piece
     (name  AssassinR)
     (notation "Assassin")
     (image South "../images/Duke/ars.bmp"
            North "../images/Duke/arn.bmp")
     (moves
        (move-type normal)
        (slide-2 s AssassinA) (slide-2 nw AssassinA) (slide-2 ne AssassinA)
        (order-def)
     )
  )
  (piece
     (name  BowmanA)
     (notation "Bowman")
     (image South "../images/Duke/bas.bmp"
            North "../images/Duke/ban.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 n BowmanR) (step-1 w BowmanR) (step-1 e BowmanR)
        (jump-2 w w BowmanR) (jump-2 e e BowmanR) (jump-2 s s BowmanR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank BowmanA)
        (move-type blank-north)
        (drop-blank BowmanR)
     )
  )
  (piece
     (name  BowmanR)
     (notation "Bowman")
     (image South "../images/Duke/brs.bmp"
            North "../images/Duke/brn.bmp")
     (moves
        (move-type normal)
        (step-1 n BowmanA) (step-1 sw BowmanA) (step-1 se BowmanA)
        (strike-1 nw BowmanA) (strike-1 ne BowmanA) (strike-2 n n BowmanA)
        (order-def)
     )
  )
  (piece
     (name  ChampionA)
     (notation "Champion")
     (image South "../images/Duke/cas.bmp"
            North "../images/Duke/can.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 n ChampionR) (step-1 w ChampionR) (step-1 e ChampionR) (step-1 s ChampionR)
        (jump-2 n n ChampionR) (jump-2 w w ChampionR) (jump-2 e e ChampionR) (jump-2 s s ChampionR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank ChampionA)
        (move-type blank-north)
        (drop-blank ChampionR)
     )
  )
  (piece
     (name  ChampionR)
     (notation "Champion")
     (image South "../images/Duke/crs.bmp"
            North "../images/Duke/crn.bmp")
     (moves
        (move-type normal)
        (strike-1 n ChampionA) (strike-1 w ChampionA) (strike-1 e ChampionA) (strike-1 s ChampionA)
        (jump-2 n n ChampionA) (jump-2 w w ChampionA) (jump-2 e e ChampionA) (jump-2 s s ChampionA)
        (order-def)
     )
  )
  (piece
     (name  DragoonA)
     (notation "Dragoon")
     (image South "../images/Duke/das.bmp"
            North "../images/Duke/dan.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 w DragoonR) (step-1 e DragoonR)
        (strike-2 n n DragoonR) (strike-2 nw nw DragoonR) (strike-2 ne ne DragoonR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank DragoonA)
        (move-type blank-north)
        (drop-blank DragoonR)
     )
  )
  (piece
     (name  DragoonR)
     (notation "Dragoon")
     (image South "../images/Duke/drs.bmp"
            North "../images/Duke/drn.bmp")
     (moves
        (move-type normal)
        (step-1 n DragoonA) (step-2 n n DragoonA)
        (jump-2 n nw DragoonA) (jump-2 n ne DragoonA)
        (slide-1 sw DragoonA) (slide-1 se DragoonA)
        (order-def)
     )
  )
  (piece
     (name  FootmanA)
     (notation "Footman")
     (image South "../images/Duke/fas.bmp"
            North "../images/Duke/fan.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 n FootmanR) (step-1 w FootmanR) (step-1 e FootmanR) (step-1 s FootmanR)
        (order-def)
     )
     (drops
        (move-type setup-footman)
        (drop-near)
        (move-type blank-south)
        (drop-blank FootmanA)
        (move-type blank-north)
        (drop-blank FootmanR)
     )
  )
  (piece
     (name  FootmanR)
     (notation "Footman")
     (image South "../images/Duke/frs.bmp"
            North "../images/Duke/frn.bmp")
     (moves
        (move-type normal)
        (step-1 nw FootmanA) (step-1 sw FootmanA) (step-1 ne FootmanA) (step-1 se FootmanA)
        (step-2 n n FootmanA)
        (order-def)
     )
  )
  (piece
     (name  GeneralA)
     (notation "General")
     (image South "../images/Duke/gas.bmp"
            North "../images/Duke/gan.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 n GeneralR) (step-2 w w GeneralR) (step-2 e e GeneralR) (step-1 s GeneralR)
        (jump-2 n nw GeneralR) (jump-2 n ne GeneralR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank GeneralA)
        (move-type blank-north)
        (drop-blank GeneralR)
     )
  )
  (piece
     (name  GeneralR)
     (notation "General")
     (image South "../images/Duke/grs.bmp"
            North "../images/Duke/grn.bmp")
     (moves
        (move-type normal)
        (step-1 n GeneralA) (step-1 w GeneralA) (step-2 w w GeneralA) (step-1 e GeneralA) (step-2 e e GeneralA)
        (jump-2 n nw GeneralA) (jump-2 n ne GeneralA)
        (order-def)
     )
  )
  (piece
     (name  LongbowmanA)
     (notation "Longbowman")
     (image South "../images/Duke/las.bmp"
            North "../images/Duke/lan.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 n LongbowmanR) (step-1 w LongbowmanR) (step-1 e LongbowmanR) (step-1 s LongbowmanR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank LongbowmanA)
        (move-type blank-north)
        (drop-blank LongbowmanR)
     )
  )
  (piece
     (name  LongbowmanR)
     (notation "Longbowman")
     (image South "../images/Duke/lrs.bmp"
            North "../images/Duke/lrn.bmp")
     (moves
        (move-type normal)
        (step-1 sw LongbowmanA) (step-1 se LongbowmanA)
        (strike-2 n n LongbowmanA) (strike-3 n n n LongbowmanA)
        (order-def)
     )
  )
  (piece
     (name  MarshallA)
     (notation "Marshall")
     (image South "../images/Duke/mas.bmp"
            North "../images/Duke/man.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (slide-1 w MarshallR) (slide-1 e MarshallR)
        (jump-2 nw nw MarshallR) (jump-2 ne ne MarshallR) (jump-2 s s MarshallR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank MarshallA)
        (move-type blank-north)
        (drop-blank MarshallR)
     )
  )
  (piece
     (name  MarshallR)
     (notation "Marshall")
     (image South "../images/Duke/mrs.bmp"
            North "../images/Duke/mrn.bmp")
     (moves
        (move-type normal)
        (step-1 n MarshallA) (step-1 w MarshallA) (step-1 e MarshallA)
        (step-1 nw MarshallA) (step-1 ne MarshallA) (step-1 sw MarshallA) (step-1 se MarshallA)
        (step-2 w w MarshallA) (step-2 e e MarshallA)
        (order-def)
     )
  )
  (piece
     (name  KnightA)
     (notation "Knight")
     (image South "../images/Duke/nas.bmp"
            North "../images/Duke/nan.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 w KnightR) (step-1 e KnightR) (step-1 s KnightR) (step-2 s s KnightR)
        (jump-2 n nw KnightR) (jump-2 n ne KnightR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank KnightA)
        (move-type blank-north)
        (drop-blank KnightR)
     )
  )
  (piece
     (name  KnightR)
     (notation "Knight")
     (image South "../images/Duke/nrs.bmp"
            North "../images/Duke/nrn.bmp")
     (moves
        (move-type normal)
        (step-1 sw KnightA) (step-2 sw sw KnightA)
        (step-1 se KnightA) (step-2 se se KnightA)
        (slide-1 n KnightA)
        (order-def)
     )
  )
  (piece
     (name  PikemanA)
     (notation "Pikeman")
     (image South "../images/Duke/qas.bmp"
            North "../images/Duke/qan.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 nw PikemanR) (step-2 nw nw PikemanR)
        (step-1 ne PikemanR) (step-2 ne ne PikemanR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank PikemanA)
        (move-type blank-north)
        (drop-blank PikemanR)
     )
  )
  (piece
     (name  PikemanR)
     (notation "Pikeman")
     (image South "../images/Duke/qrs.bmp"
            North "../images/Duke/qrn.bmp")
     (moves
        (move-type normal)
        (step-1 n PikemanA) (step-1 s PikemanA) (step-2 s s PikemanA)
        (strike-2 n nw PikemanA) (strike-2 n ne PikemanA)
        (order-def)
     )
  )
  (piece
     (name  Pikeman)
     (notation "Pikeman")
     (image South "../images/Duke/qs.bmp")
  )
  (piece
     (name  PriestA)
     (notation "Priest")
     (image South "../images/Duke/ras.bmp"
            North "../images/Duke/ran.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (slide-1 nw PriestR) (slide-1 ne PriestR)
        (slide-1 sw PriestR) (slide-1 se PriestR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank PriestA)
        (move-type blank-north)
        (drop-blank PriestR)
     )
  )
  (piece
     (name  PriestR)
     (notation "Priest")
     (image South "../images/Duke/rrs.bmp"
            North "../images/Duke/rrn.bmp")
     (moves
        (move-type normal)
        (step-1 nw PriestA) (step-1 ne PriestA)
        (step-1 sw PriestA) (step-1 se PriestA)
        (jump-2 nw nw PriestA) (jump-2 ne ne PriestA)
        (jump-2 sw sw PriestA) (jump-2 se se PriestA)
        (order-def)
     )
  )
  (piece
     (name  SeerA)
     (notation "Seer")
     (image South "../images/Duke/sas.bmp"
            North "../images/Duke/san.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 nw SeerR) (step-1 ne SeerR) (step-1 sw SeerR) (step-1 se SeerR)
        (jump-2 n n SeerR) (jump-2 w w SeerR) (jump-2 e e SeerR) (jump-2 s s SeerR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank SeerA)
        (move-type blank-north)
        (drop-blank SeerR)
     )
  )
  (piece
     (name  SeerR)
     (notation "Seer")
     (image South "../images/Duke/srs.bmp"
            North "../images/Duke/srn.bmp")
     (moves
        (move-type normal)
        (step-1 n SeerA) (step-1 e SeerA) (step-1 w SeerA) (step-1 s SeerA)
        (jump-2 nw nw SeerA) (jump-2 sw sw SeerA) (jump-2 ne ne SeerA) (jump-2 se se SeerA)
        (order-def)
     )
  )
  (piece
     (name  WizardA)
     (notation "Wizard")
     (image South "../images/Duke/was.bmp"
            North "../images/Duke/wan.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 nw WizardR) (step-1 ne WizardR) (step-1 sw WizardR) (step-1 se WizardR)
        (step-1 n WizardR) (step-1 e WizardR) (step-1 w WizardR) (step-1 s WizardR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank WizardA)
        (move-type blank-north)
        (drop-blank WizardR)
     )
  )
  (piece
     (name  WizardR)
     (notation "Wizard")
     (image South "../images/Duke/wrs.bmp"
            North "../images/Duke/wrn.bmp")
     (moves
        (move-type normal)
        (jump-2 nw nw WizardA) (jump-2 ne ne WizardA) (jump-2 sw sw WizardA) (jump-2 se se WizardA)
        (jump-2 n n WizardA) (jump-2 e e WizardA) (jump-2 w w WizardA) (jump-2 s s WizardA)
        (order-def)
     )
  )
  (piece
     (name  RangerA)
     (notation "Ranger")
     (image South "../images/Duke/eas.bmp"
            North "../images/Duke/ean.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (jump-2 n nw RangerR) (jump-2 n ne RangerR)
        (jump-2 w nw RangerR) (jump-2 e ne RangerR)
        (slide-1 n RangerR) (slide-1 s RangerR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank RangerA)
        (move-type blank-north)
        (drop-blank RangerR)
     )
  )
  (piece
     (name  RangerR)
     (notation "Ranger")
     (image South "../images/Duke/ers.bmp"
            North "../images/Duke/ern.bmp")
     (moves
        (move-type normal)
        (jump-2 s sw RangerA) (jump-2 s se RangerA)
        (slide-1 nw RangerA) (slide-1 ne RangerA)
        (order-def)
     )
  )
  (piece
     (name  DuchessA)
     (notation "Duchess")
     (image South "../images/Duke/uas.bmp"
            North "../images/Duke/uan.bmp"
            ?R "../images/Duke/i.bmp")
     (moves
        (move-type normal)
        (step-1 w DuchessR) (step-1 e DuchessR) (step-2 s s DuchessR)
        (order-def)
     )
     (drops
        (move-type blank-south)
        (drop-blank DuchessA)
        (move-type blank-north)
        (drop-blank DuchessR)
     )
  )
  (piece
     (name  DuchessR)
     (notation "Duchess")
     (image South "../images/Duke/urs.bmp"
            North "../images/Duke/urn.bmp")
     (moves
        (move-type normal)
        (step-1 w DuchessA) (step-1 e DuchessA) (step-2 s s DuchessA)
        (order-def)
     )
  )

  (board-setup
     (?R
       (FootmanA S1) (FootmanR R1)
       (PikemanA S2 S3 S4) (PikemanR R2 R3 R4)
       (WizardA S5) (WizardR R5)
       (SeerA S6) (SeerR R6)
       (PriestA S7) (PriestR R7)
       (MarshallA S8) (MarshallR R8)
       (KnightA S9) (KnightR R9)
       (RangerA S10) (RangerR R10)
       (GeneralA S11) (GeneralR R11)
       (ChampionA S12) (ChampionR R12)
       (BowmanA S13) (BowmanR R13)
       (LongbowmanA S14) (LongbowmanR R14)
       (DragoonA S15) (DragoonR R15)
       (AssassinA S16) (AssassinR R16)
     )
     (South 
       (Blank off 16)
       (DukeA off 1)
       (FootmanA off 3)
       (PikemanA off 3)
       (WizardA off 1)
       (SeerA off 1)
       (PriestA off 1)
       (MarshallA off 1)
       (KnightA off 1)
       (RangerA off 1)
       (GeneralA off 1)
       (ChampionA off 1)
       (BowmanA off 1)
       (LongbowmanA off 1)
       (DragoonA off 1)
       (AssassinA off 1)
     )
     (North
       (Blank off 16)
       (DukeA off 1)
       (FootmanA off 3)
       (PikemanA off 3)
       (WizardA off 1)
       (SeerA off 1)
       (PriestA off 1)
       (MarshallA off 1)
       (KnightA off 1)
       (RangerA off 1)
       (GeneralA off 1)
       (ChampionA off 1)
       (BowmanA off 1)
       (LongbowmanA off 1)
       (DragoonA off 1)
       (AssassinA off 1)
     )
  )

  (loss-condition (South North) (captured DukeA DukeR))
; (loss-condition (South North) (checkmated DukeA DukeR))
  (loss-condition (South North) stalemated)
)

(variant
  (title "Dark Knights")

  (option "include off-pieces" true)
  (option "recycle captures"   false)

  (turn-order 
     (South setup)
     (South setup)
     (South setup)
     (South setup)
     (South setup)
     (North setup)
     (North setup)
     repeat 
     (North normal)
     (South normal)
  )

  (board-def a2)

  (piece
     (name  KnightA)
     (notation "Knight")
     (image South "../images/Duke/nas.bmp"
            North "../images/Duke/nan.bmp")
     (moves
        (move-type normal)
        (step-1 w KnightR) (step-1 e KnightR) (step-1 s KnightR) (step-2 s s KnightR)
        (jump-2 n nw KnightR) (jump-2 n ne KnightR)
     )
     (drops
        (move-type setup)
        (drop-home)
     )
  )
  (piece
     (name  PikemanA)
     (notation "Pikeman")
     (image South "../images/Duke/qas.bmp"
            North "../images/Duke/qan.bmp")
     (moves
        (move-type normal)
        (step-1 nw PikemanR) (step-2 nw nw PikemanR)
        (step-1 ne PikemanR) (step-2 ne ne PikemanR)
     )
     (drops
        (move-type setup)
        (drop-home)
     )
  )

  (board-setup
     (North 
       (KnightA off 2)
     )
     (South
       (PikemanA off 5)
     )
  )

  (loss-condition (North) (pieces-remaining 0))
  (loss-condition (South) (pieces-remaining 2))
  (win-condition (South) (pieces-remaining 3 Pikeman))
)
