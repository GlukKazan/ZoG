(define step (
  $1 (verify empty?) add
))

(define jump (
  $1 (verify not-empty?)
  (while not-empty? $1)
  (verify empty?) 
  (add-partial continue-type)
))

(variant
  (title "Pyon Pyon Shogi")
  (board
        (image "../images/YoninShogi/PyonPyon.bmp")
        (grid
               (start-rectangle 18 14 58 59)
               (dimensions ("a/b/c" (41 0))
                           ("9/8/7/6/5/4/3/2/1" (0 41))
               )
               (directions (n 0 -1) (s 0 1))
        )
        (symmetry North (n s) (s n))
  )

  (piece
        (name  Pawn)
        (image South "../images/YoninShogi/Kanji/SPawn.bmp"
               North "../images/YoninShogi/Kanji/NPawn+.bmp"
        )
        (moves 
           (move-type normal-type)
           (step n) (jump n)
           (move-type continue-type)
           (jump n)
        )
  )

  (board-setup
        ( South 
           (Pawn a1 a2 a3 b1 b2 b3 c1 c2 c3)
        )
        ( North 
           (Pawn a9 a8 a7 b9 b8 b7 c9 c8 c7)
        )
  )

  (win-condition (South) (absolute-config Pawn (a9 a8 a7 b9 b8 b7 c9 c8 c7)))
  (win-condition (North) (absolute-config Pawn (a1 a2 a3 b1 b2 b3 c1 c2 c3)))
)

(game
  (title "Pyon Pyon Shogi (extended)")

  (players South North)
  (turn-order South North)

  (option "pass turn"          forced)
  (option "pass partial"       true)

  (move-sound "Audio/Pickup.wav")
  (release-sound "Audio/Pickup.wav")
  (capture-sound "")

  (move-priorities normal-type low-type)

  (board
        (image "../images/YoninShogi/Pyon.bmp")
        (grid
               (start-rectangle 18 14 58 59)
               (dimensions ("a/b/c/d/e/f/g/h/i" (41 0))
                           ("9/8/7/6/5/4/3/2/1" (0 41))
               )
               (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0))
        )
        (symmetry North (n s) (s n) (e w) (w e))
  )

  (piece
        (name  Pawn)
        (image South "../images/YoninShogi/Kanji/SPawn.bmp"
               North "../images/YoninShogi/Kanji/NPawn+.bmp"
        )
        (moves 
           (move-type normal-type)
           (step n) (step e)
           (jump n) (jump e)
           (move-type continue-type)
           (jump n) (jump e)
           (move-type low-type)
           (step s) (step w)
        )
  )

  (board-setup
        ( South 
           (Pawn a1 a2 a3 b1 b2 b3 c1 c2 c3)
        )
        ( North 
           (Pawn g9 g8 g7 h9 h8 h7 i9 i8 i7)
        )
  )

  (win-condition (South) (absolute-config Pawn (g9 g8 g7 h9 h8 h7 i9 i8 i7)))
  (win-condition (North) (absolute-config Pawn (a1 a2 a3 b1 b2 b3 c1 c2 c3)))
)
